set terminal pngcairo transparent enhanced font "Droid Sans,56" fontscale 1.0 size 3600, 2400
set output 'diffplot.png'

set multiplot

set style increment default

set datafile separator ','
paout  = 'paout.csv'
padiff = 'padiff.csv'

set size   1, 0.5
set origin 0, 0
set lmargin 8
set xtics auto
unset key
set xlabel 'Time (Î¼s)'
set ylabel 'Voltage (mV)'
set xrange [105:295] noreverse writeback
# set xrange [199:201] noreverse writeback # Faster to plot for debugging

plot \
       padiff u 1:2 w lines lw 2 lt rgb '#000000', \
       padiff u 1:3 w lines lw 2 lt rgb '#ffffff', \
       padiff u 1:($3 + $4) w lines lw 2 lt rgb '#ffffff', \
       padiff u 1:($3 - $4) w lines lw 2 lt rgb '#ffffff'

set size   0.25, 0.25
set origin 0.675, 0.575
set lmargin 0
set xtics auto 0.25
set ytics auto 50
unset key
unset xlabel
unset ylabel
set xrange [199.75:200.25] noreverse writeback

plot \
       paout u 3:4 w lines lt rgb '#c0c0c0' t '' , \
       paout u 1:2 w lines lt rgb '#000000' t '', \
       paout u 5:6 w lines lt rgb '#4d4d4d' t ''

set size   1, 0.5
set origin 0, 0.5
set lmargin 8
set xtics auto format ''
set ytics auto
unset xlabel
set key left top autotitle columnhead
set ylabel 'Voltage (mV)'
set xrange [105:295] noreverse writeback
# set xrange [199:201] noreverse writeback # Faster to plot for debugging

plot \
       paout u 3:4 w lines lt rgb '#c0c0c0' t '' , \
       paout u 1:2 w lines lt rgb '#000000' t '', \
       paout u 5:6 w lines lt rgb '#4d4d4d' t '', \
       NaN w lines lw 5 lt rgb '#c0c0c0' t 'Output', \
       NaN w lines lw 5 lt rgb '#000000' t 'Input', \
       NaN w lines lw 5 lt rgb '#8d8d8d' t 'Filtered Output'

unset multiplot

